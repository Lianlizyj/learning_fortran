C******* -FUNCTION MEMBER- *********************************************
C----
C---- THIS FUNCTION TESTS IF THE POINT XP IS INSIDE THE TRIANGLE LIMITED
C---- BY THE POINTS XK1, XK2 AND XK3
C----

      FUNCTION MEMBER (XP,XK1,XK2,XK3)
      IMPLICIT NONE

C---- INPUT VARIABLES
C---- XP            : COORDINATES OF THE POINT TO BE TESTED
C---- XK1, XK2, XK3 : COORDINATES OF THE CORNER POINTS OF THE TRIANGLE
      DOUBLE PRECISION XP(2),XK1(2),XK2(2),XK3(2)

C---- OUTPUT VARIBALES
C---- MEMBER  : INDICATES IF XP IS INSIDE THE TRIANGLE
      LOGICAL MEMBER

C---- LOCAL VARIABLES
C---- SIDEA1  : VECTOR FROM XK1 TO XK2
C---- SIDEB1  : VECTOR FROM XK1 TO XP
C---- SIDEA2  : VECTOR FROM XK2 TO XK3
C---- SIDEB2  : VECTOR FROM XK2 TO XP
C---- SIDEA3  : VECTOR FROM XK3 TO XK1
C---- SIDEB3  : VECTOR FROM XK3 TO XP
C---- ERG1    : CROSS PRODUCT OF SIDEA1 AND SIDEB1
C---- ERG2    : CROSS PRODUCT OF SIDEA2 AND SIDEB2
C---- ERG3    : CROSS PRODUCT OF SIDEA3 AND SIDEB3
      DOUBLE PRECISION SIDEA1(2),SIDEB1(2)
      DOUBLE PRECISION SIDEA2(2),SIDEB2(2)
      DOUBLE PRECISION SIDEA3(2),SIDEB3(2)
      DOUBLE PRECISION ERG1,ERG2,ERG3

      MEMBER = .FALSE.
      SIDEA1(1) = XK2(1) - XK1(1)
      SIDEA1(2) = XK2(2) - XK1(2)
      SIDEB1(1) = XP(1)  - XK1(1)
      SIDEB1(2) = XP(2)  - XK1(2)

      SIDEA2(1) = XK3(1) - XK2(1)
      SIDEA2(2) = XK3(2) - XK2(2)
      SIDEB2(1) = XP(1)  - XK2(1)
      SIDEB2(2) = XP(2)  - XK2(2)

      SIDEA3(1) = XK1(1) - XK3(1)
      SIDEA3(2) = XK1(2) - XK3(2)
      SIDEB3(1) = XP(1)  - XK3(1)
      SIDEB3(2) = XP(2)  - XK3(2)

      ERG1 = SIDEA1(1) * SIDEB1(2) - SIDEA1(2) * SIDEB1(1)
      ERG2 = SIDEA2(1) * SIDEB2(2) - SIDEA2(2) * SIDEB2(1)
      ERG3 = SIDEA3(1) * SIDEB3(2) - SIDEA3(2) * SIDEB3(1)

      IF ((ERG1 .GT. 0) .AND. (ERG2 .GT. 0) .AND. (ERG3 .GT. 0)) THEN
        MEMBER = .TRUE.
      ENDIF

      END
